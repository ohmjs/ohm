/*
  A grammar for parsing a grammar definition of the form used in the ECMA-262
  spec (the JavaScript language standard).

  See https://github.com/rbuckton/grammarkdown for more information.
 */
Grammarkdown {
  Productions = Production*
  Production = nonterminal "::" RightHandSide -- lexical
             | nonterminal ParameterList? ":" RightHandSide -- syntactic

  nonterminal = upper alnum*
  RightHandSide = oneof terminal+ -- oneOf
                | ">" proseSentence -- prose
                | (~Production rhsSentence)+ -- alternatives
  proseSentence = (spacesNoNL word)+
  rhsSentence = (spacesNoNL term)+
  term = 
    | application "?" -- opt
    | application -- nonterminal
    | terminal -- terminal
    | "[" applySyntactic<AssertionContents> "]" -- assertion

  application =
    | nonterminal butNotCondition -- withCondition
    | nonterminal "[" applySyntactic<ListOf<argument, ",">> "]" -- withArgs
    | nonterminal -- basic

  basicTerm = terminal | nonterminal
  literal = "<" upper+ ">"

  butNotCondition =
    | " but not " basicTerm -- basic
    | " but not one of " listOf<basicTerm, " or "> -- oneOf

  AssertionContents =
    | "empty" -- empty
    | Lookahead -- lookahead
    | "no" nonterminal "here" -- noSymbolHere
    /* lexical-goal - not used in es2015 */
    | "+" parameter -- paramSet
    | "~" parameter -- paramCleared
    | ">" (~"]" any)+ -- prose

  Lookahead =
    | "lookahead" "==" terminal -- positive
    | "lookahead" "!=" terminal -- negative
    | "lookahead" "<-" LookaheadSet -- positiveSet
    | "lookahead" "<!" LookaheadSet -- negativeSet
    | "lookahead" "<!" nonterminal -- negativeNonterminal

  LookaheadSet = "{" NonemptyListOf<terminal+, ","> "}"

  ParameterList = "[" NonemptyListOf<parameter, ","> "]"
  parameter = upper letter*

  argument = "+" parameter -- set
           | "?" parameter -- pass

  word = (letter | "\"" | "_" | ",")+
  terminal = "```" -- backtick
           | quote (~quote any)+ quote -- other
           | literal -- literal

  atDefine = "@define" (~newline any)*
  comment = "//" (~newline any)*

  spacesNoNL = ~newline spaces
  space += atDefine | comment

  newline = "\n" | "\r\n"
  quote = "`"
  oneof = "one of"
}
